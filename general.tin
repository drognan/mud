#session rod mud.rod.org 3000

#ticker {tic} {save} {300}

#alias {ef} {effects;}
#alias {sh} {sheet;}
#alias {re} {reply %0}



/* ------------------------- POTION COUNTER  ---------------------------------------------- */

#VAR NUM_TRUES 0
#VAR NUM_MANAS 0
#VAR NUM_VAPORS 0
#VAR NUM_DETOX 0
#VAR NUM_RESTORES 0

#alias {draw_potions}
{
	#show {<088>Heals: <118>$NUM_TRUES} {-3} {2};
	#show {<088>Manas: <148>$NUM_MANAS} {-3} {15};
	#show {<088>Vapors: <168>$NUM_VAPORS} {-3} {28};
	#show {<088>Detox: <118>$NUM_DETOX} {-3} {41};
	#show {<088>Restores: <138>$NUM_RESTORES} {-3} {54};
}

draw_potions

#alias {cn} {count mana potions;count true cure waters;count vapor form potions;count detox potions;count restore potions}

#action {^For a total of %1 true cure waters in your inventory.$} {#VAR NUM_TRUES %1;draw_potions}
#action {^For a total of %1 mana potions in your inventory.$} {#VAR NUM_MANAS %1;draw_potions}
#action {^For a total of %1 vapor form potions in your inventory.$} {#VAR NUM_VAPORS %1;draw_potions}
#action {^For a total of %1 detox potions in your inventory.$} {#VAR NUM_DETOX %1;draw_potions}
#action {^For a total of %1 restore potions in your inventory.$} {#VAR NUM_RESTORES %1;draw_potions}

#action {^You uncork the bottle, at the same time literally smelling the power contained within.} {#math {NUM_MANAS} {$NUM_MANAS - 1};draw_potions}
#action {^With a quick flick of the wrist, you knock back the sweet syrupy liquid.} {#math {NUM_TRUES} {$NUM_TRUES - 1};draw_potions}

/* ------------------------- TELL WINDOW  ------------------------------------------------- */

#split 10 4 1 -201 1

#VARIABLE {window}
{
     {1} {}
     {2} {}
     {3} {}
     {4} {}
     {5} {}
     {6} {}
     {7} {}
     {8} {}
     {9} {}
     {10} {}
}

#ACTION {~%1 tells you%*, "%2}
{
     showtowin %1 tells you: "%2
}

#ACTION {~%1 says%*, "%2}
{
     showtowin %1 says: "%2
}

#ALIAS {showtowin}
{
     #list window del 1;
     #list window ins -1 {%0};
     #draw white crossed box 1 1 10 202 $window[-10..-1]
}

#ALIAS {test}
{
     #showme <138>Bubba tells you, "hello";
     #showme <158>Pamela says, "bye";
     #showme <178>Pamela says in Orkish, "bye";
     #showme <168>Bobbo tells you in elvish, "bli bla blo"
}		      
