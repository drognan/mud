#session rod mud.rod.org 3000

#ticker {tic} {save} {300}

#alias {ef} {effects;}
#alias {sh} {sheet;}
#alias {re} {reply %0}


#var {resist_potions} {sharp;blunt;pierce;fire;cold;electricity;poison;acid;psionic;magic}

/* ------------------------- POTION COUNTER  ---------------------------------------------- */

#VAR NUM_TRUES 0
#VAR NUM_MANAS 0
#VAR NUM_VAPORS 0
#VAR NUM_DETOX 0
#VAR NUM_RESTORES 0


#alias {draw_trues} 
{
    #format {num_trues_str} {%-12s} {<088>Heals: <118>$NUM_TRUES};
    #show {$num_trues_str} {-3} {2};
}
#alias {draw_manas} 
{
    #format {num_manas_str} {%-13s} {<088>Manas: <148>$NUM_MANAS};
    #show {$num_manas_str} {-3} {15}
}
#alias {draw_vapors} 
{

    #format {num_vapors_str} {%-12s} {<088>Vapors: <168>$NUM_VAPORS};
    #show {$num_vapors_str} {-3} {28}
}
#alias {draw_detox} 
{
    #format {num_detox_str} {%-12s} {<088>Detox: <118>$NUM_DETOX};
    #show {$num_detox_str} {-3} {41}
}
#alias {draw_restores} 
{
    #format {num_restores_str} {%-12s} {<088>Restores: <138>$NUM_RESTORES};
    #show {$num_restores_str} {-3} {54}
}

#alias {draw_potions}
{
    draw_trues;
    draw_manas;
    draw_vapors;
    draw_detox;
    draw_restores;
}

#alias {cn} {count mana potions;count true cure waters;count vapor form potions;count detox potions;count restore potions}

#action {^For a total of %1 true cure waters in your inventory.$} {#VAR NUM_TRUES %1;draw_trues}
#action {^For a total of %1 mana potions in your inventory.$} {#VAR NUM_MANAS %1;draw_manas}
#action {^For a total of %1 vapor form potions in your inventory.$} {#VAR NUM_VAPORS %1;draw_vapors}
#action {^For a total of %1 detox potions in your inventory.$} {#VAR NUM_DETOX %1;draw_detox}
#action {^For a total of %1 restore potions in your inventory.$} {#VAR NUM_RESTORES %1;draw_restores}

#action {^You uncork the bottle, at the same time literally smelling the power contained within.} {#math {NUM_MANAS} {$NUM_MANAS - 1};draw_potions}
#action {^With a quick flick of the wrist, you knock back the sweet syrupy liquid.} {#math {NUM_TRUES} {$NUM_TRUES - 1};draw_potions}
#action {^You drink a Vapor Form Potion and your body takes on the consistency of water vapor.} {#MATH NUM_VAPORS {$NUM_VAPORS - 1};draw_potions}
#action {You gulp down the potion in a matter of seconds.  Your body vibrates slightly as you feel the effects of its magic.} {#MATH NUM_RESTORES {$NUM_RESTORES - 1};draw_restores}
#action {You feel the intoxication leaving your body.} {#MATH NUM_DETOX {$NUM_DETOX - 1};draw_detox}



/* ------------------------- TELL WINDOW  ------------------------------------------------- */

#split 10 4 1 -201 1

#VARIABLE {window}
{
     {1} {}
     {2} {}
     {3} {}
     {4} {}
     {5} {}
     {6} {}
     {7} {}
     {8} {}
     {9} {}
     {10} {}
}

#ACTION {~%1 {tells|asks} you%*, "%2}
{
     showtowin %1 tells you: "%2
}

#ACTION {~%1 says%*, "%2}
{
     showtowin %1 says: "%2
}

#ALIAS {showtowin}
{
     #list window del 1;
     #list window ins -1 {%0};
     #draw white crossed box 1 1 10 202 $window[-10..-1]
}

#ALIAS {test}
{
     #showme <138>Bubba tells you, "hello";
     #showme <158>Pamela says, "bye";
     #showme <178>Pamela says in Orkish, "bye";
     #showme <168>Bobbo tells you in elvish, "bli bla blo"
}		      
