#LIST {BREW_LIST} {CLEAR}

#VAR {POTION_BOT} {0}
#VAR {CURRENT_POTION} {}

#ALIAS {brew_potion} 
{
    #IF {!$CURRENT_POTION && BREW_LIST[] == 0}
    {
        #ECHO Nothing to do, exiting;
        #RETURN;
    };

    #ECHO {brewing..};
    #IF {&CURRENT_POTION[] == 0 && &BREW_LIST[] > 0}
    {
        #ECHO {Init item...};
        #VAR {CURRENT_POTION} {$BREW_LIST[1]};
        #LIST {BREW_LIST} {DELETE} {1};
    };
    #ELSEIF {$CURRENT_POTION[num] == 0 && &BREW_LIST[] > 0} 
    {
        #ECHO {Next item...};
        #VAR {CURRENT_POTION} {$BREW_LIST[1]};
        #LIST {BREW_LIST} {DELETE} {1};
    };
    #ELSEIF {$CURRENT_POTION[num] == 0 && &BREW_LIST[] == 0}
    {
        #ECHO {No Potions in Queue};
        #VAR {CURRENT_POTION} {};
        #RETURN;
    };

    #LOCAL {num} {$CURRENT_POTION[num]};
    #LOCAL {type} {$CURRENT_POTION[type]};
    #ECHO {.. $num $type};
   
    #IF {"$type" == "{strength|dexterity|constitution|wisdom|intelligence|detox|fire|cold|acid|magic|psionic|poison|sharp|blunt|pierce|electricity|cyan}"} 
    {
        #MATH {num_potions} {$CURRENT_GP / 200};
        #MATH {num_potions} {@clamp{$num_potions;$num}};

        #ECHO {Brewing $num_potions $type};
    
        #IF {$num_potions > 0} {
            detach $num_potions potion;
            #IF {"$type" == "cyan"} {create $num_potions mana} {create $num_potions $type};
        };
    };
    #IF {"$type" == "mana"}
    {
        #MATH {num_potions} {$CURRENT_GP / 1100};
        #MATH {num_potions} {@clamp{$num_potions;$num}};

        #ECHO {Brewing $num_potions $type};

        #IF {$num_potions > 0} {
            detach $num_potions potion;
            cast mana potion $num_potions;
        };
    };
    #IF {"$type" == "true"}
    {
        #MATH {num_potions} {$CURRENT_GP / 1000};
        #MATH {num_potions} {@clamp{$num_potions;$num}};

        #ECHO {Brewing $num_potions $type};

        #IF {$num_potions > 0} {
            detach $num_potions potion;
            cast true cure water $num_potions;
        };
    };
    #IF {"$type" == "restore"}
    {
        #MATH {num_potions} {$CURRENT_GP / 1100};
        #MATH {num_potions} {@clamp{$num_potions;$num}};

        #ECHO {Brewing $num_potions $type};

        #IF {$num_potions > 0} {
            detach $num_potions potion;
            cast restore water $num_potions;
        };
    };
    #IF {"$type" == "vapor"}
    {
        #MATH {num_potions} {$CURRENT_GP / 1750};
        #MATH {num_potions} {@clamp{$num_potions;$num}};

        #ECHO {Brewing 1 $type};

        #IF {$num_potions > 0} {
            detach potion;
            cast vapor form potion;
        };
    }
}

#ACTION {You are too mentally drained to create anything right now.} {dm}

#ACTION {You have accidentally created %1 Baneful %2 {Potion|Potions}.} {drop baneful potions;bury baneful potions}

#ACTION {You are the proud owner of %1 %2 {Potion|Potions}.} 
{
    #MATH {REMAINING} {$CURRENT_POTION[num] - %1};
    #VARIABLE {CURRENT_POTION} {{num}{$REMAINING} {type}{$CURRENT_POTION[type]}};
    drop %2 potions;
}

#ACTION {You have created %1 Mana {Potion|Potions}.} 
{
    #MATH {REMAINING} {$CURRENT_POTION[num] - %1};
    #VARIABLE {CURRENT_POTION} {{num}{$REMAINING} {type}{$CURRENT_POTION[type]}};
    drop mana potions;
}

#ACTION {You have created %1 True Cure {Water|Waters}.} 
{
    #MATH {REMAINING} {$CURRENT_POTION[num] - %1};
    #VARIABLE {CURRENT_POTION} {{num}{$REMAINING} {type}{$CURRENT_POTION[type]}};
    drop true cure waters;
}

#ACTION {You have created %d {Potion|Potions} of Restore Water.} 
{
    #MATH {REMAINING} {$CURRENT_POTION[num] - %1};
    #VARIABLE {CURRENT_POTION} {{num}{$REMAINING} {type}{$CURRENT_POTION[type]}};
    drop restore waters;
}

#ACTION {You finish an incantation and a spell of dizziness overcomes you.  Magic springs foward from your hands, transforming the water inside the potion into a rich red liquid.} 
{
    #MATH {REMAINING} {$CURRENT_POTION[num] - 1};
    #VARIABLE {CURRENT_POTION} {{num}{$REMAINING} {type}{$CURRENT_POTION[type]}};
    put vapor form potion in bp;
}


#ALIAS {ttt} 
{
    order_potion 14 int;
    order_potion 14 con;
    order_potion 14 str;
    order_potion 14 dex;
    order_potion 1 resistset;
}


#ALIAS {unbox} {withdraw 50 bottle;put bottles in bp}
#ACTION {You drop an empty crate to the ground and kick it away.} {put bottles in bp;unbox}
#ALIAS {convert} 
{

    #MATH {num_bottles} {$CURRENT_GP / 200};
    detach $num_bottles bottle;
    cast fill bottle max;
}
#ACTION {In this manner you successfully create %1 Water {Potion|Potions}.} 
{
    put water potions in bp;
    convert;
}
#ACTION {You are currently too mentally drained to cast Fill Bottle.}
{
    put bottles in bp;
    dm;
    convert;
}


#ALIAS {order_potion %1 %2} 
{
    #ECHO {%1 - %2};

    #IF {"%2" == "{vapor|cyan|mana|int|con|str|dex|wis|true|resistset|restore|detox|fire|cold|acid|magic|psionic|poison|sharp|blunt|pierce|electricity}"}
    {   
        #LOCAL {ORDER_TYPE} {%2};

        #IF {"$ORDER_TYPE" == "int"} {#LOCAL {ORDER_TYPE} {intelligence}};
        #IF {"$ORDER_TYPE" == "con"} {#LOCAL {ORDER_TYPE} {constitution}};
        #IF {"$ORDER_TYPE" == "str"} {#LOCAL {ORDER_TYPE} {strength}};
        #IF {"$ORDER_TYPE" == "dex"} {#LOCAL {ORDER_TYPE} {dexterity}};
        #IF {"$ORDER_TYPE" == "wis"} {#LOCAL {ORDER_TYPE} {wisdom}};
        #IF {"$ORDER_TYPE" == "resistset"}
        {
            order_potion %1 blunt;
            order_potion %1 sharp;
            order_potion %1 pierce;
            order_potion %1 fire;
            order_potion %1 cold;
            order_potion %1 acid;
            order_potion %1 magic;
            order_potion %1 psionic;
            order_potion %1 poison;
            order_potion %1 electricity;
        };
        
        #IF {"$ORDER_TYPE" != "resistset"} 
        {
            #LIST {BREW_LIST} {ADD} {{{num}{%1}{type}{$ORDER_TYPE}}};
        };
    };
    #ELSE 
    {
        #ECHO {Valid potions: cyan|mana|int|con|str|dex|wis|true|resistset|restore|detox|fire|cold|acid|magic|psionic|poison|sharp|blunt|pierce|electricity}
    }
}


#ALIAS {order_clear} {#LIST {BREW_LIST} {CLEAR};#VARIABLE {CURRENT_POTION} {}}
#ALIAS {order_list} 
{
    #FOREACH {$BREW_LIST[%*]} {order_line} {#SHOW $order_line[num]: $order_line[type]} 
}

#ALIAS {potion_bot %1} 
{ 
    #IF {"%1" == "off"}
    {
        #VAR {POTION_BOT} {0};
    };
    #ELSEIF {"%1" == "on"}
    {
        #VAR {POTION_BOT} {1};
    };
    #ELSEIF {"%1" == "auto"}
    {
        #VAR {POTION_BOT} {2};
    };

    #ECHO $POTION_BOT;
  
    #ECHO {Potion bot: @on_off_color{$POTION_BOT}};
}


#FUNCTION {on_off_color} 
{
    #IF {"%1" == "off" || %1 == 0}
    {
        #RETURN <018>OFF;
    };
    #IF {"%1" == "on" || %1 == 1}
    {
        #RETURN <028>ON;
    };
    #IF {"%1" == "auto" || %1 == 2}
    {
        #RETURN <068>AUTO;
    };

    #RETURN <123>N/A;
}


#FUNCTION {clamp}
{
    #IF {%1 > %2} {#RETURN %2};
    #RETURN %1;

}

